# enemy
!Расчет атаки противника

if $ARGS[0]='start':
cla
!в этом блоке на основании переменной выбирается программа атаки противника
     if program = 0:
     !программа действий простых атакеров физухой
     xgt'enemy','fighter'
     end
     if program = 1:
     !программа действий физуха и ножи
     xgt'enemy','fighter2'
     end
     if program = 2:
     !программа действий физуха, ножи, туман
     xgt'enemy','fighter3'
     end
     if program = 3:
     !программа действий физуха, ножи, туман, клоны
     xgt'enemy','fighter4'
     end
     if program = 4:
     !программа действий физуха, ножи, туман, клоны, паралич
     xgt'enemy','fighter5'
     end
     if program = 5:
     !программа действий Кисаме
     xgt'enemy','kisame'
     end
     if program = 6:
     !программа действий Тоби
     xgt'enemy','tobi'
     end
     if program = 7:
     !программа действий Тоби
     xgt'enemy','gedomazo'
     end
     if program = 8:
     !программа действий Огнестрела
     xgt'enemy','gun'
     end
     if program = 9:
     !программа действий Сасори
     xgt'enemy','sasori'
     end
     if program = 10:
     !программа действий Конан
     xgt'enemy','konan'
     end
     if program = 11:
     !программа действий Хидан
     xgt'enemy','hidan'
     end

end

if $ARGS[0]='hidan':
cla
     if zerkaloV > 0:set barierV = 0
     if zerkaloV <= 0:set barierV = 5500
if healthV < vitalV*10 + strenV*5:set healthV = vitalV*10 + strenV*5
     if zerkaloV > 0:
	 set healthV = vitalV*10 + strenV*5
	 set mannaV = intelV*magikV + magikV*100 + vitalV*10
	 set willpowerV = willV*10
	 set health = health - 400
	 set manna = manna - 500
     '<B><font color=red><<$nameV>> орет "РИТУАЛ НАЧИНАЕТСЯ!" и встает в круг начертанный кровью на земле.</font></B>'
     '<B><font color=red>Он протыкает себя и вы чувствуете, как боль пронзает тело.</font></B>'
     xgt'fight','sta'
     exit     
     end
     if zerkaloV <= 0 and defence > 0:
	 set damTipV = 5000
	 set bonusShV = 100
	 set hidanAtk = 1
     '<B><font color=red><<$nameV>> орет "ТВОЕ СУЩЕСТВОВАНИЕ ОСКОРБЛЯЕТ МОЕГО БОГА ДЖАШИНА!" и швыряет в вас свою трехлезвийную косу управляя ей при помощи каната.</font></B>'
     if klon > 0:set klon = klon/2&'<B><font color=red><<$nameV>> орет "МРАЗЬ! Я ЗАРЕЖУ ТЕБЯ КАК СВИНЬЮ!"</font></B>'
     xgt'atakA1','atak'
     exit     
     end
     if zerkaloV <= 0 and defence <= 0:
	 set damTipV = 300
	 set bonusShV = 100
	 set hidanAtk = 1
     '<B><font color=red><<$nameV>> орет "ТЫ УМРЕШЬ БОГОХУЛЬНИК!" и швыряет в вас свою трехлезвийную косу управляя ей при помощи каната.</font></B>'
     if klon > 0:set klon = 0&'<B><font color=red>Ваши клоны превратились в пар от удара косы. <<$nameV>> орет "Я БЕССМЕРТНЫЙ ОПУСТОШИТЕЛЬ! А ТЫ ГОВНО!"</font></B>'
     if tuman > 0:set tuman = 0&'<B><font color=red><<$nameV>> орет "ТЫ ЖАЛОК В СВОИХ ТЩЕТНЫХ ПОПЫТКАХ СПРЯТАТСЯ ОТ МЕНЯ!"</font></B>'
     xgt'atakA1','atak'
     exit     
     end
end

if $ARGS[0]='konan':
cla
     if round < 6:
     set mannaV = mannaV - 10
	 set damTipV = 500
     '<B><font color=red><<$nameV>> складывает знаки руками и в вас летят листы бумаги.</font></B>'
     if klon > 0:set klon = 0&'<B><font color=red>Ваши клоны превратились в пар от удара бумаги.</font></B>'
     xgt'atakA1','atak'
     exit
     end
     if round >= 6:
		 if unmaterialV > 5 and defence > 0:
		 set mannaV = mannaV - 250
		 set damTipV = 15000
		 set bonusShV = 100
         '<B><font color=red><<$nameV>> взмывает в воздух на белоснежных крыльях и вырываясь из ее крыльев бумага обрушивается на вас целым потоком летящих с страшной скоростью бумажных мечей.</font></B>'
		 if klon > 0:set klon = 0&'<B><font color=red>Ваши клоны превратились в пар от удара бумаги.</font></B>'
		 xgt'atakA1','atak'
		 exit
		 end
		 if unmaterialV > 5 and defence <= 0:
		 set mannaV = mannaV - 20
		 set damTipV = 450
         '<B><font color=red><<$nameV>> создает бумажные бумеранги которые летят в вашу сторону.</font></B>'
         xgt'atakA1','atak'
		 exit
		 end
		 if defenceV <= 0 and unmaterialV <= 5:
		 set mannaV = mannaV - 250
		 set defenceV = defenceV + 3000
		 set defenceMV = defenceMV + 3000
		 set defenceWV = defenceWV + 3000
		 '<B><font color=red>Конан взмывает вверх на своих бумажных крыльях как ангел. Вокруг нее начинает кружится бумага создавая ей защиту.</font></B>'
		 xgt'fight','sta'
		 exit
		 end
		 if unmaterialV <= 2 and konanD < 3:
		 set mannaV = mannaV - 150
		 set konanD = konanD + 1
		 '<B><font color=red>Конан замирает в воздухе медитируя.</font></B>'
		 xgt'fight','sta'
		 exit
		 end
		 if konanD >= 3:
		 set mannaV = mannaV - 250
		 set konanD = 0
		 set unmaterialV = 20
		 '<B><font color=red>Конан рассыпается в воздухе на тысячи листов бумаги.</font></B>'
		 xgt'fight','sta'
		 exit
		 end
     end
end

if $ARGS[0]='sasori':
cla
if SasoriBody > 1:set damTipV = 1000
if SasoriBody = 1:set damTipV = 2000
set poisonAV = 1
set defenceV = defenceV + 10
set defenceMV = defenceMV + 20
set defenceWV = defenceWV + 50
set mannaV = mannaV - 10
	 if SasoriBody = 1 and defence > 0:
	 set defSasori = defence*20/100
	 set defence = defence - defSasori
	 '<B><font color=red>Сверху падает дождь из стальных игл сильно ослабляя вашу защиту.</font></B>'
	 end 
'<B><font color=red><<$nameV>> стреляет в вас целым облаком ядовитых шипов.</font></B>'
if klon > 0:set klon = 0&'<B><font color=red>Ваши клоны превратились в пар от удара игл.</font></B>'
xgt'atakA1','atak'
end

if $ARGS[0]='gedomazo':
cla
     if klon <= 0 and tuman <= 0:
	 set magweapbonusV = 5000
	 '<B><font color=red><<$nameV>> с бешеным ревом бьет по вам своим огромным каменным кулаком.</font></B>'
	 xgt'atak','enemy'
	 exit
	 end
	 if klon > 0 or tuman > 0:
	 set klon = 0
	 set tuman = 0
	 set mannaV = mannaV - 500
	 '<B><font color=red><<$nameV>> издает страшный рык и ударная волна воздуха проносится над полем боя.</font></B>'
	 xgt'fight','sta'
	 exit
	 end

end

if $ARGS[0]='tobi':
cla
!программа атаки Тоби
     if unmaterialV <= 0 and IzanagiV = 0 and GedoMazo = 0:
     *clr
     '<B><font color=red><<$nameV>> приложил пальцы к губам и тихо произнес "КУЧИЕ СЕН НО ДЖИТСУ".</font></B>'
     '<B><font color=red>Перед вами появилась гиганская каменная статуя демона и бросилась на вас в атаку.</font></B>'
     set GedoMazo = 1
     set $nameV = 'Гедо Мазо'
	 !Статы
	 set strenV = 1000
	 set speedV = 100
	 set agilV = 90
	 set reactV = 100
	 set vitalV = 5000
	 set intelV = 50
	 set willV = 50
	 !Навыки
	 set magikV = 1
	 set boxingV = 100
	 set shootV = 100
	 !Параметры
	 set healthV = vitalV*10 + strenV*5
	 set mannaV = intelV*magikV + magikV*100 + vitalV*10
	 set willpowerV = willV*10
	 set BonusAtakV = 0
	 set BonusDefV = 0
	 !программа действий
	 set barierV = 1500
	 set program = 7
	 xgt'fight','start'
     exit
     end
     
     if unmaterialV <= 2 and IzanagiV = 0 and GedoMazo > 0 and TobiStena = 0:
     set mannaV = mannaV - 500
     set TobiStena = 1
     set defenceV = defenceV + 25000
     '<B><font color=red><<$nameV>> делает магический знак и его кожа чернеет упрочняя его тело и создавая защиту в 25000 единиц.</font></B>'
     set barierV = 2000
     xgt'fight','sta'
     exit
     end
     
     if unmaterialV <= 2 and IzanagiV = 0 and GedoMazo > 0 and TobiStena = 1:
     set mannaV = mannaV + 6000
     set TobiStena = 2
     '<B><font color=red><<$nameV>> делает магический знак и вы чувствуете как его магическая энергия резко возрастает.</font></B>'
     xgt'fight','sta'
     exit
     end
     
     
     if stun > 0:
     set magweapbonusV = 2000
     '<B><font color=red><<$nameV>> победоносно произнес, "ТЕПЕРЬ ТЫ ТРУП".</font></B>'
	 '<B><font color=red>Он хватает вас за горло рукой и поднимает в воздух.</font></B>'
	 '<B><font color=red>Его пальцы будто из стали и они сдавливают ваше горло со страшной силой.</font></B>'
	 xgt'atak','enemy'
	 exit
     end
     
     if defence >= 5000:
     set defence = 0
     set defenceM = 0
     set defenceActPar = 0
     set defenceActParM = 0
     set defenceAct = 0
     set defenceActM = 0
     set defAtk = 0
     set mannaV = mannaV - 100
     set stunV = 1
     '<B><font color=red><<$nameV>> закрыл глаз и вы увидели, из него как слеза потекла капля крови.</font></B>'
     '<B><font color=red>Он открыл глаз ставший абсолютно красным с черными прожилками и произнес АМАТЕРАСУ</font></B>'
     '<B><font color=red>В вашу сторону устремилось черное всепожирающее пламя молниеносно уничтожившее вашу защиту.</font></B>'
     xgt'fight','sta'
     exit
     end
     if tuman > 0:
     set tumanV = tumanV + tuman
     set tuman = 0
     '<B><font color=red><<$nameV>> складывает магические знаки руками и ваш туман переходит под его контроль застилая вам обзор.</font></B>'
	 '<B><font color=red>Он атакует вас.</font></B>'
	 xgt'atak','enemy'
	 exit
     end
     if unmaterialV > 0 and klon <= 0:
	 set stunnerV = 1
	 set mannaV = mannaV - 20
	 set magweapbonusV = 800
	 '<B><font color=red><<$nameV>> атакует вас собрав манну в руку.</font></B>'
	 xgt'atak','enemy'
	 exit
     end
     if unmaterialV > 0 and klon > 0:
     set klon = 0
     set stunV = 1
	 '<B><font color=red><<$nameV>> складывает знаки руками и все ваши клоны просто превращаются в пар.</font></B>'
	 xgt'atak','enemy'
	 exit
     end
     if unmaterialV <= 0 and tobizanagi < 2:
     set mannaV = mannaV - 250
     set tobizanagi = tobizanagi + 1
     !set unmaterialV = 10
     '<B><font color=red><<$nameV>> складывает знаки руками.</font></B>'
     xgt'fight','sta'
     exit
     end
     if unmaterialV <= 0 and tobizanagi = 2:
     set mannaV = mannaV - 250
     set tobizanagi = 0
     set unmaterialV = 20
     set stunV = 1
     '<B><font color=red><<$nameV>> складывает знаки руками и вокруг его глаза закручивается вихрь.</font></B>'
     xgt'fight','sta'
     exit
     end

end

if $ARGS[0]='kisame':
cla
!программа атаки Кисаме
     if defenceV <= 0 and kisamePro = 0:
     set kisamePro = 1
     set mannaV = mannaV - 150
     set defenceV = defenceV + 1500
     set defenceMV = defenceMV + 1500
     set defenceWV = defenceWV + 1500
     set defenceActParV = 500
     set defenceActParMV = 500
     set defenceActV = 10
     set defenceActMV = 10
     '<B><font color=red><<$nameV>> складывает знаки руками и вокруг его образуется водяная сфера защищающая его от повреждений.</font></B>'
     xgt'fight','sta'
     exit
     end
     if defenceV <= 1000 and kisamePro > 0:
     set mannaV = mannaV - 500
     set defenceV = defenceV + 5000
     set defenceMV = defenceMV + 5000
     set defenceWV = defenceWV + 5000
     set defenceActParV = 500
     set defenceActParMV = 500
     set defenceActV = 10
     set defenceActMV = 10
     set defAtkMV = 10
     '<B><font color=red><<$nameV>> складывает знаки руками и пространство вокруг начинает погружатся в воду.</font></B>'
     if klon > 0:set klon = 0&'<B><font color=red>Ваши клоны превратились в пар от удара воды.</font></B>'
     xgt'fight','sta'
     exit
     end
     if defenceV > 1000:
     set kisameRAND = RAND (0,3)
         if kisameRAND = 0:
         set klonV = klonV + 3
         '<B><font color=red><<$nameV>> складывает знаки руками.</font></B>'
         xgt'fight','sta'
         exit
         end 
         if kisameRAND > 0:
         set mannaV = mannaV - 250
		 set damTipV = 1500
		 set damTipMV = 2500
		 set bonusShV = 50
         '<B><font color=red><<$nameV>> складывает знаки руками и в вас летят акулы созданные из воды.</font></B>'
         if klon > 0:set klon = 0&'<B><font color=red>Ваши клоны превратились в пар от удара воды.</font></B>'
         xgt'atakA1','atak'
         exit
         end
     end
     '<B><font color=red><<$nameV>> атакует.</font></B>'
     xgt'enemy','fighter'
end

if $ARGS[0]='gun':
cla
!непосредственно программа атаки
!gs'atak'
set damTipV = 1500
'<B><font color=red><<$nameV>> стреляет в вас.</font></B>'
xgt'atakA1','atak'
end

if $ARGS[0]='fighter':
cla
!непосредственно программа атаки
!gs'atak'
xgt'atak','enemy'
end

if $ARGS[0]='fighter2':
cla
!непосредственно программа атаки
set deystrand = RAND (0,1)
     if deystrand = 0:
     xgt'atak','enemy'
     end
     if deystrand = 1:
     set knifeV = RAND (1,3)
     '<B><font color=red><<$nameV>> метает в вас <<knifeV>> ножей</font></B>'
     xgt'atak','enemy'
     end
end

if $ARGS[0]='fighter3':
cla
!непосредственно программа атаки
     if tuman = 0 and tumanV < 5 and mannaV > 10:
     set mannaV = mannaV - 10
     set tumanV = tumanV + 10
     '<B><font color=red><<$nameV>> выпускает туман на поле боя.</font></B>'
     xgt'fight','sta'
     exit
     end
set deystrand = RAND (0,1)
     if deystrand = 0:
     xgt'atak','enemy'
     end
     if deystrand = 1:
     set knifeV = RAND (1,3)
     '<B><font color=red><<$nameV>> метает в вас <<knifeV>> ножей</font></B>'
     xgt'atak','enemy'
     end
end

if $ARGS[0]='fighter4':
cla
!непосредственно программа атаки
     if tuman = 0 and tumanV < 5 and mannaV > 10:
     set mannaV = mannaV - 10
     set tumanV = tumanV + 10
     '<B><font color=red><<$nameV>> выпускает туман на поле боя.</font></B>'
     xgt'fight','sta'
     exit
     end
     if klonV < 1 and mannaV > 15:
     set mannaV = mannaV - 15
     set klonV = klonV + 1
     '<B><font color=green>ПРОМАХ</font></B>'
     xgt'fight','sta'
     exit
     end
set deystrand = RAND (0,1)
     if deystrand = 0:
     xgt'atak','enemy'
     end
     if deystrand = 1:
     set knifeV = RAND (1,3)
     '<B><font color=red><<$nameV>> метает в вас <<knifeV>> ножей</font></B>'
     xgt'atak','enemy'
     end
end

if $ARGS[0]='fighter5':
cla
!непосредственно программа атаки
     if tuman = 0 and tumanV < 5 and mannaV > 10:
     set mannaV = mannaV - 10
     set tumanV = tumanV + 10
     '<B><font color=red><<$nameV>> выпускает туман на поле боя.</font></B>'
     xgt'fight','sta'
     exit
     end
     if klonV < 1 and mannaV > 15:
     set mannaV = mannaV - 15
     set klonV = klonV + 1
     '<B><font color=green>ПРОМАХ</font></B>'
     xgt'fight','sta'
     exit
     end
     if stun < 1 and manna > 20:
     set stunnerV = 1
     set mannaV = mannaV - 20
     '<B><font color=red><<$nameV>> атакует вас парализующим ударом.</font></B>'
     xgt'atak','enemy'
     end
set deystrand = RAND (0,1)
     if deystrand = 0:
     xgt'atak','enemy'
     end
     if deystrand = 1:
     set knifeV = RAND (1,3)
     '<B><font color=red><<$nameV>> метает в вас <<knifeV>> ножей</font></B>'
     xgt'atak','enemy'
     end
end





--- enemy ---------------------------------

