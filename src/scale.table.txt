# scale.table
!'
"
Отрисовка шкалы с помощью таблицы:
"

$func ('scale.table',275,420)+' 275/420'
$func ('scale.table',400,420)+' 400/420'
$func ('scale.table',420,420)+' 420/420'
$func ('scale.table',157,420)+' 157/420'
$func ('scale.table',42,420)+'  42/420'
$func ('scale.table',0,420)+'   0/420'

'


! -----scale.table------
args[0] = args[0]	&	! текущее значение параметра
args[1] = args[1]	&	! максимальное значение параметра
if args[2] = 0: args[2] = 200	&	! ширина шкалы (таблицы) в пикселях. По умолчанию 350 px
if $args[3] = "": $args[3]="FF0000"	&	! цвет заполненной части шкалы в формате RRGGBB. По умолчанию - красный
! сначала "создаём" соответствующую таблицу
$args['table']='<TABLE width=<<args[2]>> border=0 cellspacing=0 cellpadding=0><TR>'
! вычисляем ширину в процентах заполненной части
args['x'] = (args[0] * 100) / args[1]
! вычисляем ширину в процентах незаполненной части
args['empty'] = 100 - args['x']
! Если ширина заполненной части больше нуля процентов, добавляем в таблицу ячейку с указанным цветом (красный по-умолчанию)
if args['x']>0:
	$args['table']+="<TD width=<<args['x']>>% bgcolor=#"+$args[3]+">&nbsp;</TD>"
end
! Если ширина незаполненной части шкалы больше нуля процентов, добавляем в таблицу ячейку с другим цветом (серый).
if args['empty']>0:
	$args['table']+="<TD width=<<args['empty']>>% bgcolor=#888888>&nbsp;</TD>"
end
! "закрываем" таблицу
$args['table']+='</TR></TABLE>'
! результат
$result = $args['table']
--- scale.table ---------------------------------

