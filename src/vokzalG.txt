# vokzalG
$metka = $ARGS[0]
$loc = $CURLOC
'<center><H4>Вокзал</H4></center>'
'<center><img src="pic\vokzal.jpg" ></center>'
'Маленькая тихая станция. Около <a href="exec:minut+=3&GT ''vokzalGin''">вокзала</a> стоит <a href="exec:minut+=5&GT ''gkafe''">кафешка</a>. Так же возле вокзала находится маленький отель для приезжих, почтовое отделение и отдел милиции.'
clr
gs'stat'
gs'time'

if kidalob=0:
podrand=rand(0,3)
     if money>=5000 and hour>=8 and hour<=20 and podrand=0 and monthKidalo!month:
     cls
     monthKidalo=month
     gs'stat'
     '<center><H4>Вокзал</H4></center>'
     '<center><img src="pic\vokzal.jpg" ></center>'
     'Вы идете по вокзалу и к вам подходит девушка с микрофоном и мужчина с телекамерой. Девушка обращается к вам "Здраствуйте. Мы с телевиденья. Сейчас проходит беспрецедентная акция - шоу по раздаче подарков. Мы выбрали именно вас." Девушка достает красиво обернутую коробку с бантом и дает вам. "Вот тут ноутбук последнего поколения."'
     act 'Уйти':minut+=15&gt $curloc
         act 'Взять коробку':
         cls
         gs'stat'
         '<center><H4>Вокзал</H4></center>'
         '<center><img src="pic\vokzal.jpg" ></center>'
         'Вы взяли коробку и мужчина стал снимать вас на камеру. Девушка подставила микрофон и предложила передать приветы родным и близким. Вы стоя с тяжелой коробкой перед камерой передали приветы и поблагодарили за врученный подарок. Мужчина все снял и девушка предложила вам оплатить ндс, со стоимости подарка. Это всего каких то 5ть тысяч.'
             act 'Оплатить ндс':
			 cls
			 money-=5000
			 kidalob=1
			 gs'stat'
			 '<center><H4>Вокзал</H4></center>'
			 '<center><img src="pic\vokzal.jpg" ></center>'
			 'Вы отдали деньги и стали распечатывать подарок. Когда вам удалось вскрыть коробку, вы увидели, что там лежит кирпич обернутый газетой. Вы огляделись в поисках девушки и мужчины, но их уже и след простыл.'
				 act 'Уйти':minut+=15&gt $curloc 
			 end 
         end
     exit
     end
end

if kidalo=0:
podrand=rand(0,3)
     if money>=1000 and hour>=8 and hour<=20 and podrand=0 and glamur>0 and monthKidalo!month:
     cls
     monthKidalo=month
     '<center><H4>Вокзал</H4></center>'
     '<center><img src="pic\vokzal.jpg" ></center>'
     'Идя по вокзалу вы замечаете как возле вокзала стоят двое мужчин. Они замечают вас и подходят к вам разговаривая между собой. "Ну просто фотомодель, ну прямо Клаудия Шиффер какая-то..." Один из парней, в представительном костюме подходит к вам и говорит. "Мы открываем в вашем городе новый магазин и нам нужна девушка для рекламы. Мы предлагаем вам 30 тысяч за фотографию на рекламу."'
     act 'Уйти':minut+=15&gt $curloc
         act 'Согласиться':
         cls
         gs'stat'
         'Вы охотно соглашаетесь, на такое заманчивое предложение. Парни отвели вас к стройплощадке рядом с вокзалом, один из парней достал фотоаппарат и поставил его напротив дерева с кустами. "Снимать будем на натуральном фоне. Так живописней. Только вот проблема, ваше платье бликует. Вам нужно переодеться." Парень достал из багажника машины платье и подал вам. "Переодеться можете прямо в машине" Парень показал вам ауди которая стояла не по далеку.'
         act 'Уйти':minut+=15&gt $curloc
             act 'Переодеться':
			 cls
			 gs'stat'
			 dress[numdress]=0
			 set numdress=0
			 set odetjeans=0
			 set odetyoga=0
			 set odetsarafan=1
			 set odetshort=0
			 set odetskirt=0
			 set odetdress=0
			 set odetprofi=0
			 set odetpants=0
			 set odetlatex=0
			 set odethooker=0
			 set odetkis=0
			 set odetnewclo=0
			 set odetero=0
			 set vatnik=0
			 set glamur = 0
			 set sport = 0
			 set ski = 0
			 set hook = 0
			 set ofclo = 0
			 set sexual = 0
			 if cumfrot > 0:set cumfrot = 0
			 set nude = 0
			 set odevnesh = 0
			 !
			 sarafan[3]=2
			 sarafanH[3]=20
			 'Вы залезли в машину и сняли платье. После этого вы одели на себя платье, которое вам дал мужчина.'
			     act 'Позировать':
			     cls
			     kidalo=1
			     gs'stat'
			     'Вы вышли из машины оставив свои вещи в машине и подошли к дереву. Один из парней встал за фотоаппарат, второй суетится возле вас, показывая вам, как надо стоять. Фотоаппарат щелкнул. "Так, а теперь фотография со спины" Вы встали спиной к фотоаппарату и услышали голос. "Отлично. Тепрь чуть приподними голову и закрой глаза." Вы исполнили указания, но никакого щелча фотоаппарата не последовало. Вместо этого вдруг заработал двигатель машины.'
			         act 'Обернуться':
					 cls
					 money=0
					 gs'stat'
					 'Вы обернулись и увидели, что никакого фотоаппарата нет. Оба мужчины сидят в ауди, которая тронулась с места и рванула по улицам. Вы лихорадочно сообразили, что свое платье и деньги вы оставили в машине. Вас кинули как последнюю лохушку.'
					 act 'Уйти':minut+=15&gt $curloc
					 end
			     end
			 end
         end
     exit
     end
end

gs'family'




act 'Уйти в городок':minut+=15&gt'gorodok'

act 'Выйти на северное шоссе':minut+=15&nroad=20&gt'road'


--- vokzalG ---------------------------------

