# init
!Расчет инициативы
!Обязательно проверять наличие станов
!hod переменная отвечающая за то кто будет атаковать
!hod = 0 игрок атакует hod = 1 противник атакует
!<font color=red><<$name>> </font>

if GedoMazo = 1 and stunV > 0:set stunV = 0&set mannaV = mannaV - 1000&'<font color=red><<$nameV>> с оглушительным ревом, выделяет большое количество энергии позволяющей ему снова вас атаковать.</font>'

set initi = react*20/100
set initi = initi - dospehVes
if initi < 0:set initi = 0
set init = RAND (react-initi,react+initi) + initBonus
set initiV = reactV*20/100
set initiV = initiV - dospehVesV
if initiV < 0:set initiV = 0
set initV = RAND (reactV-initiV,reactV+initiV) + initBonusV

if hodtime >= 3 and stun = 0 and stunV = 0 and initBonus = 0:
!игрок уже трижды атаковал
'<font color=red><<$nameV>> перехватил инициативу</font>'
set hod = 1
set hodtime = 0
exit
end
if hodtimeV >= 3 and stun = 0 and stunV = 0 and initBonusV = 0:
!противник уже трижды атаковал
'<font color=green>Вы перехватили инициативу</font>'
set hod = 0
set hodtimeV = 0
exit
end


if stun = 0 and stunV = 0 and hodtime < 3 and hodtimeV < 3 or stun = 0 and stunV = 0 and initBonus > 0 or stun = 0 and stunV = 0 and initBonusV > 0:
!оба не в стане
     if tuman = 0 and tumanV = 0:
     !нет тумана
         if init > initV:
         !инициативу захватил игрок
         '<font color=green>Вы захватили инициативу</font>'
         set hod = 0
         set hodtime = hodtime + 1
         exit
         end
         if init < initV:
         !инициативу захватил противник
         '<font color=red><<$nameV>> захватил инициативу</font>'
         set hod = 1
         set hodtimeV = hodtimeV + 1
         exit
         end
         if init = initV:
         set hod = RAND (0,1)
         if hod = 0:'<font color=green>Вы с трудом захватили инициативу</font>'
         if hod = 1:'<font color=red><<$nameV>> с трудом захватил инициативу</font>'
         exit 
         end
     end
     if tuman > 0:
     !туман игрока
     set tuman = tuman - 1
     set initV = reactV-initiV
         if init > initV:
         !инициативу захватил игрок
         '<font color=green>В вашем тумане <<$nameV>> не успевает за вами и вы захватили инициативу</font>'
         set hod = 0
         set hodtime = hodtime + 1
         exit
         end
         if init < initV:
         !инициативу захватил противник
         '<font color=red>Не смотря на ваш туман <<$nameV>> захватил инициативу</font>'
         set hod = 1
         set hodtimeV = hodtimeV + 1
         exit
         end
         if init = initV:
         set hod = RAND (0,1) 
         if hod = 0:'<font color=green>Вы с трудом захватили инициативу не смотря на ваш туман</font>'
         if hod = 1:'<font color=red><<$nameV>> не смотря на ваш туман все таки захватил инициативу</font>'
         exit
         end
     end
     if tumanV > 0:
     !туман противника
     set tumanV = tumanV - 1
     set init = react-initi
         if init > initV:
         !инициативу захватил игрок
         '<font color=green>Не смотря на туман, вы захватили инициативу</font>'
         set hod = 0
         set hodtime = hodtime + 1
         exit
         end
         if init < initV:
         !инициативу захватил противник
         '<font color=red><<$nameV>> используя свой туман легко захватывает инициативу</font>'
         set hod = 1
         set hodtimeV = hodtimeV + 1
         exit
         end
         if init = initV:
         set hod = RAND (0,1) 
         if hod = 0:'<font color=green>Вы с трудом захватили инициативу не смотря на туман</font>'
         if hod = 1:'<font color=red><<$nameV>> используя свой туман захватил инициативу</font>'
         exit
         end
     end
end

if stun > 0:
!игрок в стане
set stun = stun - 1
set hod = 1
if tuman > 0:set tuman = tuman - 1
if tumanV > 0:set tumanV = tumanV - 1
'<font color=red>Вы не можете двигаться и <<$nameV>> легко захватывает инициативу</font>'
exit
end

if stunV > 0:
!противник в стане
set stunV = stunV - 1
set hod = 0
if tuman > 0:set tuman = tuman - 1
if tumanV > 0:set tumanV = tumanV - 1
'<font color=green><<$nameV>> не может двигаться и вы легко захватываете инициативу</font>'
exit
end

--- init ---------------------------------

